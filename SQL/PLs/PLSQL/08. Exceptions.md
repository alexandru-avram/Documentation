# Exceptions

An exception is a runtime error or unexpected event that disrupts the normal flow of a PL/SQL block.

Oracle automatically raises exceptions for things like:
- No rows returned
- Unique constraint violation
- Division by zero

You can also raise your own exceptions.


## Exception Block Structure

The `EXCEPTION` block is optional, and comes after the `BEGIN` block, usually at the end:

```
DECLARE
  -- variable declarations
BEGIN
  -- executable statements
EXCEPTION
  WHEN exception1 THEN
    -- handler for exception1
  WHEN exception2 THEN
    -- handler for exception2
  WHEN OTHERS THEN
    -- generic handler
END;
```

## Types of Exceptions

```
| Type               | Description                                    |
| ------------------ | ---------------------------------------------- |
| **Predefined**     | Built into Oracle (e.g., `NO_DATA_FOUND`)      |
| **Non-predefined** | Oracle-defined but not automatically available |
| **User-defined**   | Declared and raised manually by the programmer |
```

### Predefined Exceptions

| Exception          | Raised When...                                     |
| ------------------ | -------------------------------------------------- |
| `NO_DATA_FOUND`    | A `SELECT INTO` returns **no rows**                |
| `TOO_MANY_ROWS`    | A `SELECT INTO` returns **more than one row**      |
| `ZERO_DIVIDE`      | Division by zero occurs                            |
| `INVALID_NUMBER`   | Conversion from string to number fails             |
| `DUP_VAL_ON_INDEX` | Insert/update violates a unique constraint         |
| `VALUE_ERROR`      | Assignment causes overflow, conversion error, etc. |
| `OTHERS`           | A **catch-all** for any uncaught exceptions        |

### Non-Predefined Exceptions

Non-predefined exceptions are Oracle-defined errors that:
- Have error codes
- But don’t have built-in names

You need to explicitly declare and associate them using `PRAGMA EXCEPTION_INIT`.

Example: `-2292` is the Oracle error code for foreign key constraint violation

```
DECLARE
  e_constraint_violation EXCEPTION;
  PRAGMA EXCEPTION_INIT(e_constraint_violation, -2292);
```

### User-Defined Exceptions
You can define and raise your own exceptions.

```
DECLARE
  e_invalid_bonus EXCEPTION;
BEGIN
  IF bonus < 0 THEN
    RAISE e_invalid_bonus;
  END IF;
EXCEPTION
  WHEN e_invalid_bonus THEN
    DBMS_OUTPUT.PUT_LINE('Bonus must be positive.');
END;
```

## Accessing Error Details
Use built-in functions inside the `WHEN OTHERS` block:

```
WHEN OTHERS THEN
  DBMS_OUTPUT.PUT_LINE('Error Code: ' || SQLCODE); -- Returns the error message
  DBMS_OUTPUT.PUT_LINE('Error Message: ' || SQLERRM); -- Returns the Oracle error number
```

## Reraising Exceptions
You can handle an error (e.g., log it) and then re-raise it using `RAISE`;

```
EXCEPTION
  WHEN OTHERS THEN
    log_error(SQLCODE, SQLERRM);
    RAISE;
```

## Scope and Propagation
If an exception is not caught in the current block:
- It bubbles up to the calling block
- If no outer block handles it → session throws an unhandled exception

```
-- Outer block catches what inner does not
BEGIN
  BEGIN
    -- Inner block with missing handler
    SELECT salary INTO v_sal FROM employees WHERE id = -1;
  END;
EXCEPTION
  WHEN NO_DATA_FOUND THEN
    DBMS_OUTPUT.PUT_LINE('Handled at outer level.');
END;
```

## Final Template

```
DECLARE
  e_custom EXCEPTION;
BEGIN
  -- main logic
  IF some_error THEN
    RAISE e_custom;
  END IF;
EXCEPTION
  WHEN e_custom THEN
    -- handle specific
  WHEN NO_DATA_FOUND THEN
    -- handle data errors
  WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('Error: ' || SQLERRM);
    RAISE;  -- optional
END;
```
