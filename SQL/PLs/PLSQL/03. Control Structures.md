# Control Structures

Control structures are essential for making PL/SQL programs dynamic and powerful.

* `IF/ELSIF/ELSE`	Decision making
* `CASE`	Cleaner decision making
* `LOOP`	Infinite loop (until EXIT)
* `FOR LOOP`	Fixed-iteration looping
* `WHILE LOOP`	Pre-condition looping
* `CONTINUE`	Skip to next loop iteration
* `EXIT` Leaves the entire loop
* `GOTO`	Jump to label (rarely recommended)


## IF Statements

Used to make decisions and execute blocks based on conditions.

```
IF condition THEN
   -- statements
ELSIF another_condition THEN
   -- statements
ELSE
   -- fallback statements
END IF;
```

Multiple `ELSIF` allowed, but only one ELSE (optional).

```
DECLARE
  v_salary NUMBER := 5000;
BEGIN
  IF v_salary > 10000 THEN
    DBMS_OUTPUT.PUT_LINE('High salary');
  ELSIF v_salary > 5000 THEN
    DBMS_OUTPUT.PUT_LINE('Moderate salary');
  ELSE
    DBMS_OUTPUT.PUT_LINE('Low salary');
  END IF;
END;
```

## CASE Statement
A cleaner way to replace multiple `IF/ELSIF` conditions.

### Simple CASE

```
CASE expression
  WHEN value1 THEN
    -- statements
  WHEN value2 THEN
    -- statements
  ELSE
    -- fallback
END CASE;
```

### Searched CASE

```
CASE
  WHEN condition1 THEN
    -- statements
  WHEN condition2 THEN
    -- statements
  ELSE
    -- fallback
END CASE;
```

## LOOPS

Repeats forever unless explicitly exited. Always use an `EXIT WHEN` to avoid infinite looping.

```
LOOP
   -- statements
   EXIT WHEN condition;
END LOOP;
```

```
DECLARE
  v_counter NUMBER := 1;
BEGIN
  LOOP
    DBMS_OUTPUT.PUT_LINE('Counter: ' || v_counter);
    v_counter := v_counter + 1;
    EXIT WHEN v_counter > 5;
  END LOOP;
END;
```

### FOR Loop
Repeats a fixed number of times — most commonly used than `WHILE` loops.

```
FOR counter_variable IN [REVERSE] lower_bound..upper_bound LOOP
  -- statements
END LOOP;
```

```
BEGIN
  FOR i IN 1..5 LOOP
    DBMS_OUTPUT.PUT_LINE('Iteration: ' || i);
  END LOOP;
END;
```

You can also use a `REVERSE 5..1` to loop downward.

### WHILE Loop

Repeats while a condition is true.

```
WHILE condition LOOP
  -- statements
END LOOP;
```

```
DECLARE
  v_counter NUMBER := 1;
BEGIN
  WHILE v_counter <= 5 LOOP
    DBMS_OUTPUT.PUT_LINE('Counter: ' || v_counter);
    v_counter := v_counter + 1;
  END LOOP;
END;
```

### Nested Loops
Loops inside other loops. Useful for multi-dimensional processing (e.g., rows and columns).

```
BEGIN
  FOR i IN 1..3 LOOP
    FOR j IN 1..2 LOOP
      DBMS_OUTPUT.PUT_LINE('i=' || i || ', j=' || j);
    END LOOP;
  END LOOP;
END;
```


## CONTINUE
Skips the current iteration and jumps to the next iteration of the loop.

```
CONTINUE WHEN condition;
```

Cleaner than writing `IF` statements inside loops.


```
BEGIN
  FOR i IN 1..5 LOOP
    CONTINUE WHEN MOD(i,2) = 0; -- Skip even numbers
    DBMS_OUTPUT.PUT_LINE('Odd Number: ' || i);
  END LOOP;
END;
```

## EXIT
`EXIT` is used to immediately terminate a loop.  It completely stops the loop execution and continues after the loop block. It can be either a simple basic `EXIT`, or used in a combination with a condition as an `EXIT WHEN`.

### Basic EXIT
If you use just `EXIT;`, it will exit as soon as that line is reached, no condition needed.

```
DECLARE
  v_counter NUMBER := 1;
BEGIN
  LOOP
    DBMS_OUTPUT.PUT_LINE('Counter: ' || v_counter);
    v_counter := v_counter + 1;
    EXIT;  -- Exit after first iteration
  END LOOP;
END;
```

### Conditional EXIT WHEN
Exits only if a certain condition is TRUE. Very useful for ensuring a loop is not infinite and doesn't break your script.

```
DECLARE
  v_counter NUMBER := 1;
BEGIN
  LOOP
    DBMS_OUTPUT.PUT_LINE('Counter: ' || v_counter);
    v_counter := v_counter + 1;
    EXIT WHEN v_counter > 5;
  END LOOP;
END;
```


## GOTO Statement
Jumps to a label in the block. Use with caution — can make code messy, avoid unless really necessary.

```
GOTO label_name;

<<label_name>>
-- statements
```

Can be used for emergency exits but bad for readability.

```
DECLARE
  v_value NUMBER := 1;
BEGIN
  IF v_value = 1 THEN
    GOTO skip_message;
  END IF;

  DBMS_OUTPUT.PUT_LINE('This will not be printed');

<<skip_message>>
  DBMS_OUTPUT.PUT_LINE('Jumped here directly');
END;
```

## Combine Control Structured Elements
You can combine EXIT, CONTINUE, and LOOP effectively.

```
FOR i IN 1..10 LOOP
  IF i = 5 THEN
    CONTINUE; -- Skip 5
  ELSIF i = 8 THEN
    EXIT;     -- Stop completely at 8
  ELSE
    DBMS_OUTPUT.PUT_LINE(i);
  END IF;
END LOOP;
```
